  DEFINT A-Z
DECLARE FUNCTION GetBillCycle% ()
DECLARE SUB RefundMenu ()
DECLARE SUB RefundDeposit ()
DECLARE FUNCTION GetCustMeterType% (UBLocatRec() AS ANY, ThisMeter%)
DECLARE SUB UtilAdjustments ()
DECLARE SUB RePrintUtilBills ()
DECLARE SUB PostBillTrans ()
DECLARE FUNCTION GetAdjFactor# ()
DECLARE FUNCTION IsDeleted% (CustAcctNo&)
DECLARE SUB ClearScrn ()
DECLARE SUB UBBillMenu ()
DECLARE SUB BillPrintMenu ()
DECLARE SUB LoadUBSetUpFile (UBSetUpRec() AS ANY, UBSetupLen%)
DECLARE SUB PrintUtilBills ()
DECLARE SUB MakeSequenceIndex (IndexText$)
DECLARE FUNCTION AskAbandonPrint% ()
  
DECLARE FUNCTION FindRateTbl% (RATECODE$, NumOfRates%, UBRateTbls() AS ANY)
DECLARE FUNCTION GetNumOfRevs% ()
DECLARE FUNCTION GetRevCharge# (RateTbl AS ANY, TMeterConsp&, MeterMulti&)
DECLARE SUB MakeBillFile (AbortFlag, FuelAdjAmt#, WhatCycle)
DECLARE FUNCTION Round# (N#)
DECLARE SUB PrintRptFile (RptTitle$, FileName$, LPTPort%, RetCode%, EntryPoint%)
DECLARE SUB MakeZipCodeIndex ()
DECLARE SUB SortT (SEG Element AS ANY, NumElements%, Direction%, StructSize%, MemberOff%, MemberSize%)
DECLARE SUB ShowProcessingScrn (RptTitle$)
DECLARE SUB MakePostalIndex (IndexText$)
DECLARE SUB ShowPctComp (BYVAL RecNo%, BYVAL NumOfRecs%)
DECLARE SUB MakeTempIndex (Indextype%)
DECLARE SUB PreBillReport ()
DECLARE SUB DisplayUBScrn (ScrnName$)
DECLARE SUB ManualMeterReading ()
DECLARE SUB PressButton (BYVAL KeyCode%, BYVAL ButtonRow%, BYVAL ButtonLCol%, BYVAL ButtonRCol%)
DECLARE SUB EnterMeterReadingsMenu ()
DECLARE FUNCTION GetNumOfCust% ()
DECLARE FUNCTION GetNumRateRecs% ()
DECLARE FUNCTION Exist% (FileName$)
DECLARE SUB PrintHelp (H$)
DECLARE SUB PrintTitle (Title$)
DECLARE SUB PIProcessMenu (JrnlType%)
DECLARE FUNCTION MsgBox% (LibName$, FormName$)
DECLARE FUNCTION AskSavePayment
DECLARE FUNCTION QPTrim$ (Text$)
DECLARE FUNCTION FileSize& (FileName$)
DECLARE FUNCTION Monitor% ()
DECLARE SUB ShowCursor ()
DECLARE SUB HideCursor ()
DECLARE SUB KillFile (FileName$)
  
  '$INCLUDE: 'DefCnf.BI'
DECLARE SUB TitleBox (Row%, LeftCol%, BoxWidth%, Title$, Cnf AS ANY)
DECLARE SUB VertMenu (Item$(), Choice, MaxLen, BoxBot, Ky$, Action, Cnf AS Config)
DECLARE SUB FGetAH (FileName$, SEG Element AS ANY, ElSize%, NumEls%)
DECLARE SUB FOpenS (FileName$, Handle%)
DECLARE SUB FClose (Handle%)
DECLARE SUB FCreate (FileName$)
DECLARE SUB FGetRTA (Handle%, SEG Dest AS ANY, RecNo&, RecSize%)
DECLARE SUB FPutRTA (Handle%, SEG Dest AS ANY, RecNo&, RecSize%)
DECLARE SUB FPutAH (FileName$, SEG Element AS ANY, ElSize%, NumEls%)
DECLARE SUB QPrintRC (Text$, Row, Col, Kolor)
DECLARE SUB SortStr2 (BYVAL Address%, Size%, Direction%)
DECLARE FUNCTION FUsing$ (Number$, Image$)
DECLARE FUNCTION Date2Num% (DateString$)
DECLARE FUNCTION Num2Date$ (DateNum%)
DECLARE FUNCTION QPValL& (LongNum$)
DECLARE SUB MPaintBox (TRow%, LCol%, BRow%, RCol%, Kolor%)
DECLARE SUB StuffBuf (Key2Stuff$)
DECLARE SUB ShowCustHistory (CUSTACCT&)
DECLARE SUB CursorOff ()
DECLARE SUB WaitForAction ()
DECLARE SUB BlockClear ()
DECLARE SUB SaveScrn (ScrnArray())
DECLARE SUB RestScrn (ScrnArray())
DECLARE SUB LookUp (CUSTACCT&, Text$, Parm1, Parm2, ActiveOnly%)
DECLARE FUNCTION PromptSaveData ()
  
  '$INCLUDE: 'formedit.BI'
  '$INCLUDE: 'fieldinf.BI'
  '$INCLUDE: 'qscr.BI'
  '$INCLUDE: 'SetCnf.BI'
  '$INCLUDE: 'ubtrans.BI'
  '$INCLUDE: 'ubdraft.BI'
  '$INCLUDE: 'ubsetup.bi'
  '$INCLUDE: 'PageInfo.bi'
  '$INCLUDE: 'newcust.bi'
  '$INCLUDE: 'ubrate.BI'
  '$INCLUDE: 'ubpaymnt.BI'
  
  CONST False = 0, True = NOT False
  
  TYPE DraftRptType
    TRANSIT  AS STRING * 9
    BankName AS STRING * 34
    CUSTACCT AS STRING * 8
    CustName AS STRING * 35
    BillAmt  AS STRING * 10
    BANKACCT AS STRING * 20
  END TYPE
  
  TYPE BDRptType
    BankName  AS STRING * 14
    CustRec   AS INTEGER
    TransRec  AS LONG
  END TYPE

SUB BankDraftRegister
  
  Dash80$ = STRING$(125, "-")
  
  'load setup file
  REDIM UBSetUpRec(1) AS UBSetupRecType
  LoadUBSetUpFile UBSetUpRec(), UBSetupLen
  TownName$ = UBSetUpRec(1).UTILNAME
  
  MaxLines = 60
  
  REDIM MChoice$(1 TO 6)
  
  PageNo = 0
  
  IndexName$ = NameIndexFile
  OKFlag = True
  
  REDIM DFTRec(1) AS DraftRptType
  REDIM UBCustRec(1) AS NewUBCustRecType
  UBCustRecLen = LEN(UBCustRec(1))
  
  REDIM UBTransRec(1) AS UBTransRecType
  UBTransRecLen = LEN(UBTransRec(1))
  
  '*********************************
  LibName$ = "UB"
  ScrnName$ = "DRFTINFO"
  
  '--define the multi-choice fields
  '--Initialize the form name array
  NumFlds = LibNumberOfFields(LibName$, ScrnName$) + 1
  
  '--define Quick Screen form editing arrays
  REDIM Frm(1) AS FormInfo
  REDIM Form$(NumFlds, 2)
  REDIM Fld(NumFlds) AS FieldInfo
  
  '--for each screen, get first and last fields
  StartEl = 0
  
  LibGetFldDef LibName$, ScrnName$, StartEl, Fld(), Form$(), ErrCode
  Action = 1
  Frm(1).StayOnField = True
  
  '--Set screen number to one and display screen
  
  BlockClear
  
  DisplayUBScrn ScrnName$
  
  DO
    
    EditForm Form$(), Fld(), Frm(1), Cnf, Action
    
    '--Check for Key presses
    SELECT CASE Frm(1).KeyCode
    CASE F10Key, 13
      BDate = Date2Num(Form$(1, 0))
      ExitFlag = True
    CASE EscKey
      ABExit = True
      ExitFlag = True
    END SELECT
    
    '--check for mouse clicks on buttons not attached to the form
    IF Frm(1).Presses THEN
      SELECT CASE Frm(1).MRow
      CASE 14   'Look for the f10 or esc button
        SELECT CASE Frm(1).MCol
        CASE 29 TO 40           '--Cancel button
          PressButton 27, 14, 29, 40
        CASE 43 TO 54           '--F10 button
          PressButton 13, 14, 43, 54
        END SELECT
      END SELECT                'row
    END IF
    
  LOOP UNTIL ExitFlag
  
  ERASE Frm, Form$, Fld
  
  IF ABExit THEN
    GOTO AbortExit
  END IF
  
  '*********************************
  
  UBTFile = FREEFILE
  OPEN "UBTRANS.DAT" FOR RANDOM SHARED AS UBTFile LEN = UBTransRecLen
  
  NumOfRecs = LOF(UBTFile) \ UBTransRecLen
  
  UBCust = FREEFILE
  OPEN "UBCUST.DAT" FOR RANDOM SHARED AS UBCust LEN = UBCustRecLen
  
  UBRpt = FREEFILE
  OPEN "UBANKDFT.RPT" FOR OUTPUT AS UBRpt
  
  BlockClear
  ShowProcessingScrn "Processing Bank Draft Report"
  
  GOSUB PrintBankDFTHeader
  
  FOR Cnt& = 1 TO NumOfRecs
    GET UBTFile, Cnt&, UBTransRec(1)
    IF (UBTransRec(1).TransType = TranUtilityBill) AND (UBTransRec(1).TransDate = BDate) THEN   'and THEN
      GET UBCust, UBTransRec(1).CustAcctNo, UBCustRec(1)
      IF UBTransRec(1).TransAmt > 0 THEN
        IF LineCnt > MaxLines THEN
          PRINT #UBRpt, CHR$(12)
          GOSUB PrintBankDFTHeader
        END IF
        IF (UBCustRec(1).UseDraft = "Y") OR (LEN(QPTrim$(UBCustRec(1).BankName)) > 0) THEN
          CstCnt = CstCnt + 1
          REDIM PRESERVE BDCust(1 TO CstCnt) AS BDRptType
          BDCust(CstCnt).BankName = QPTrim$(UBCustRec(1).BankName)
          BDCust(CstCnt).CustRec = UBTransRec(1).CustAcctNo
          BDCust(CstCnt).TransRec = Cnt&
        END IF
      END IF
    END IF
    IF AskAbandonPrint% THEN
      ABExit = True
      GOTO NON2PrintExit:
    END IF
    ShowPctComp Cnt&, NumOfRecs
DFTskipem:
  NEXT
  
  IF CstCnt <= 0 THEN
    PRINT #UBRpt, "No Bills found for: "; Num2Date$(BDate)
    PRINT #UBRpt, Dash80$
    GOTO NON2PrintExit
  END IF
  
  SortT BDCust(1), CstCnt, 0, 20, 0, 14
  
  Split = CstCnt
  PrevBank$ = BDCust(1).BankName
  FOR Cnt = 1 TO CstCnt
    ThisBank$ = BDCust(Cnt).BankName
    IF ThisBank$ <> PrevBank$ THEN
      Split = Cnt - 1
      EXIT FOR
    END IF
  NEXT
  
  IF Split < CstCnt THEN
    SortT BDCust(1), Split, 0, 20, 14, -1
    SortT BDCust(Split + 1), CstCnt - Split, 0, 20, 14, -1
  END IF
  '(SEG Element AS ANY, NumElements%, Direction%, StructSize%, MemberOff%, MemberSize%)
  
  GET UBCust, BDCust(1).CustRec, UBCustRec(1)
  PrevBank$ = BDCust(1).BankName
  
  FOR Cnt = 1 TO CstCnt
    GET UBCust, BDCust(Cnt).CustRec, UBCustRec(1)
    GET UBTFile, BDCust(Cnt).TransRec, UBTransRec(1)
    ThisBank$ = BDCust(Cnt).BankName
    IF ThisBank$ <> PrevBank$ THEN
      PageNo = 0
      PRINT #UBRpt, Dash80$
      PRINT #UBRpt, CHR$(12)
      GOSUB PrintBankDFTHeader
      PrevBank$ = ThisBank$
    END IF
    IF LineCnt > MaxLines THEN
      PRINT #UBRpt, CHR$(12)
      GOSUB PrintBankDFTHeader
    END IF
    LSET DFTRec(1).TRANSIT = QPTrim$(UBCustRec(1).TRANSIT)
    LSET DFTRec(1).BankName = QPTrim$(UBCustRec(1).BankName)
    RSET DFTRec(1).CUSTACCT = QPTrim$(STR$(BDCust(Cnt).CustRec))
    LSET DFTRec(1).CustName = QPTrim$(UBCustRec(1).CustName)
    LSET DFTRec(1).BANKACCT = QPTrim$(UBCustRec(1).BANKACCT)
    LSET DFTRec(1).BillAmt = FUsing$(STR$(UBTransRec(1).TransAmt), "#####.##")
    PRINT #UBRpt, DFTRec(1).TRANSIT; "  "; DFTRec(1).BankName;
    PRINT #UBRpt, "  "; DFTRec(1).CUSTACCT; "  "; DFTRec(1).CustName;
    PRINT #UBRpt, "  "; DFTRec(1).BillAmt; "  "; "  "; DFTRec(1).BANKACCT
    LineCnt = LineCnt + 1
    IF AskAbandonPrint% THEN
      ABExit = True
      GOTO NON2PrintExit:
    END IF
    ShowPctComp Cnt, CstCnt
  NEXT
  
  PRINT #UBRpt, Dash80$
  PRINT #UBRpt, CHR$(12)
  
NON2PrintExit:
  CLOSE

  IF ABExit THEN
    GOTO AbortExit
  END IF

  ERASE UBSetUpRec, MChoice$, DFTRec, UBCustRec, UBTransRec
  
  LPTPort = 1
  IF NOT AbortFlag THEN
    PrintRptFile "Bank Draft Register Report", "UBANKDFT.RPT", LPTPort, RetCode, EntryPoint
  END IF
  
AbortExit:
  EXIT SUB
  
PrintBankDFTHeader:
  PageNo = PageNo + 1
  PRINT #UBRpt, "Utility Billing Bank Draft Register. "; TAB(55); TownName$; TAB(110); "Page: "; PageNo
  PRINT #UBRpt, "Date: "; DATE$
  PRINT #UBRpt, "Bank No.  Bank Name"; TAB(48); "Acct No.   Customer Name                      Draft Amt      Bank Acct No."
  PRINT #UBRpt, Dash80$
  LineCnt = 5
  
  RETURN
END SUB

